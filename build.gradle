buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.11'
        classpath 'net.sf.proguard:proguard-gradle:6.0.3'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'

group 'jp.kentan.minecraft.nekocore'
version '4.0.10'

sourceCompatibility = 1.8

configurations {
    embed
    compile.extendsFrom(embed)
}

repositories {
    mavenCentral()

    maven {
        name = "Spigot"
        url "https://hub.spigotmc.org/nexus/content/groups/public"
    }

    maven {
        name 'Vault'
        url 'http://nexus.hc.to/content/repositories/pub_releases'
    }

    maven {
        name 'bintray-nuvotifier-repo'
        url 'https://dl.bintray.com/nuvotifier/maven/'
    }

    maven {
        name "sk89q-repo"
        url "http://maven.sk89q.com/repo/"
    }
}

dependencies {
    embed "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    embed 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.1'

    compile 'org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT'

    compile 'net.milkbowl.vault:VaultAPI:1.6'

    compile 'com.vexsoftware:nuvotifier-bukkit:2.3.4'

    compile 'me.lucko.luckperms:luckperms-api:4.3'

    compile 'com.sk89q.worldguard:worldguard-legacy:6.2'
    compile 'com.sk89q.worldedit:worldedit-bukkit:6.1.4-SNAPSHOT'

    compile files('libs/ShopChest-1.13-SNAPSHOT.jar')

    embed 'com.squareup.okhttp3:okhttp:3.12.0'
    embed 'com.squareup.retrofit2:retrofit:2.5.0'
    embed 'com.squareup.retrofit2:converter-moshi:2.5.0'

    embed 'org.jsoup:jsoup:1.11.3'

    embed 'org.mariadb.jdbc:mariadb-java-client:2.3.0'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    from { configurations.embed.collect { it.isDirectory() ? it : zipTree(it) } }
}

task proguard(type: proguard.gradle.ProGuardTask, dependsOn: jar) {
    injars jar.archivePath

    configuration 'proguard.txt'

    outjars("$buildDir/libs/${project.name}-${project.version}_proguard.jar")
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}